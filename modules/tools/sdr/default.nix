{
  pkgs,
  lib,
  config,
  ...
}:
let
  gui = config.environment.sysConf.gui.enable;
  cliWrapped = pkgs.symlinkJoin {
    name = "sdrCliTools";
    paths = with pkgs; [
      uhd
      soapysdr-with-plugins
    ];
    nativeBuildInputs = [ pkgs.makeWrapper ];
    postBuild = ''
      cp -r --remove-destination ${./uhd}/* "$out/share/uhd/images"
      for bin in $out/bin/*; do
        wrapProgram "$bin" \
          --set UHD_IMAGES_DIR "$out/share/uhd/images"
      done
    '';
  };
  guiWrapped = pkgs.symlinkJoin {
    name = "sdrGuiTools";
    paths = with pkgs; [
      gnuradio
      gqrx
      abracadabra
      sdrpp
    ];
    nativeBuildInputs = [ pkgs.makeWrapper ];
    postBuild = ''
      for bin in $out/bin/*; do
        wrapProgram "$bin" \
          --set UHD_IMAGES_DIR "${cliWrapped}/share/uhd/images" \
          --set SOAPY_SDR_PLUGIN_PATH="${cliWrapped}/lib/SoapySDR/modules0.8"
      done
    '';
  };
in
{
  config = lib.mkIf (pkgs.stdenv.hostPlatform.system == "x86_64-linux") {
    environment.systemPackages = [
      cliWrapped
    ]
    ++ lib.optionals gui [
      guiWrapped
    ];

    services.udev.packages = with pkgs; [
      uhd
    ];
  };
}
